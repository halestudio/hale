// Fraunhofer Institute for Computer Graphics Research (IGD)
// Department Graphical Information Systems (GIS)
//
// Copyright (c) 2004-2013 Fraunhofer IGD. All rights reserved.
//
// This source code is property of the Fraunhofer IGD and underlies
// copyright restrictions. It may only be used with explicit
// permission from the respective owner.

apply plugin: 'download-task'
apply plugin: 'java'
apply from: 'gradle/tycho.gradle'
apply from: 'gradle/commitStage.gradle'
apply from: 'gradle/integrationTestStage.gradle'
apply from: 'gradle/productionStage.gradle'
// apply from: 'gradle/checkDependencies.gradle'
// apply from: 'gradle/deployArtifacts.gradle'

import org.eclipse.core.runtime.internal.adaptor.EclipseEnvironmentInfo
import java.text.DateFormat
import java.text.SimpleDateFormat

group = 'eu.esdihumboldt'

Calendar calendar = Calendar.getInstance();
DateFormat dateFormat = new SimpleDateFormat('YYYYMMddHHmm');

gradle.taskGraph.whenReady {
	// determine build ID
	String buildID = System.getenv()['BUILD_ID'] // set by jenkins
	if (buildID == null){
		buildID = dateFormat.format(calendar.getTime())
	}
	project.ext.buildId = buildID
}

defaultTasks 'commitStage'

ext {
    rootDir = file('..')
    parentArtifactId = 'rcp-parent'

    // true if Maven artifacts should be created instead of OSGI bundles
    generateArtifacts = false

    // suffix for bundle version numbers
    versionSuffix = ''

    // path to the platform bundle
    platformBundle = file('../platform')
	
	// name of the platform definition file (w/o extension [.target])
	platformFileName = 'hale-platform' 

    // true if MongoDB should be included in the final product
    includeMongoDB = false

    // true if InstantReality Transcoder should be included in the final product
    includeTranscoder = false

    // a list of additional files to include in the final product (default: empty)
    additionalFilesToPackage = []

    // path to the bundles to build
    bundles = [
		file('../common/plugins'),
		file('../cst/plugins'),
		file('../doc/plugins'),
		file('../io/plugins'),
		file('../server/plugins'),
		file('../ui/plugins'),
		file('../util/plugins'),
		file('../ext/styledmap'),
		file('../ext/xslt'),
		file('../ext/ageobw')
    ]
	
	// bundles to exclude for the build (symbolic names)
	excludeBundles = [
		'eu.esdihumboldt.hale.ui.application.help.internal',
		'eu.esdihumboldt.hale.ui.application.help.external'
	]
	
	/*
	 * Specify OS specific bundles. Maps symbolic names to OS
	 * identifiers.
	 * 
	 * Maven tycho has problems with platform/os specific bundles.
	 * Thus we have to filter them in the product configuration.
	 * As this happens based on the bundle symbolic names, we have
	 * to provide information for which OS each bundle should be
	 * applied.
	 * see also productionStage.gradle#filterPlatformSpecificFragments
	 * FIXME remove once maven tycho can deal with it on its own
	 */
	osSpecificBundles = [
		'eu.esdihumboldt.hale.ui.application.help.internal': ['macosx', 'win32'],
		'eu.esdihumboldt.hale.ui.application.help.external': ['linux']
	]

    // URL to the directory containing mirrored artifacts (Maven, JRE, ...)
    cs3dMirrorUrl = 'http://www.cityserver3d.de/updates2/56abe3f5559dc32c35c6a7e5e7acbcb05659132c'

    // Names of zip files containing Java Runtimes (sorted by their respective platform)
    jreArtifacts = [
        'win32': [
            'win32': [
                'x86': 'jre-7u5-windows-i586.zip',
                'x86_64': 'jre-7u5-windows-x64.zip'
            ]
        ],
        'linux': [
            'gtk': [
                'x86': 'jre-7u5-linux-i586.zip',
                'x86_64': 'jre-7u5-linux-x64.zip'
            ]
        ]
    ]

    // Names of zip files containing mongodb instances (sorted by their respective platform)
//    mongodbArtifacts = [
//        'win32': [
//            'win32': [
//                'x86_64': 'mongodb-win32-x86_64-2.2.0.zip'
//            ]
//        ],
//        'linux': [
//            'gtk': [
//                'x86_64': 'mongodb-linux-x86_64-2.2.0.tgz'
//            ]
//        ]
//    ]

    // Names of zip files containing Transcoder instances (sorted by their respective platform)
//    transcoderArtifacts = [
//        'win32': [
//            'win32': [
//                'x86': 'transcoder_0_2_6.zip',
//                'x86_64': 'transcoder_0_2_6.zip'
//            ]
//        ]
//    ]
}

if (project.ext.has('additionalConfig')) {
	applyExternalConfiguration(file(project.ext.additionalConfig))
}
else {
	applyExternalConfiguration(file('config.groovy'))
}

def applyExternalConfiguration(File configFile) {
	assert configFile.exists()
	// execute config file as groovy script...
	Binding binding = new Binding();
	
	// specify the binding for the script
	binding.setVariable('parentDir', configFile.parentFile);
	
	GroovyShell shell = new GroovyShell(binding);
	def res = shell.evaluate(configFile.text)
	
	// ... creating a closure to configure the project
	def prjClosure = binding.project.clone()
	prjClosure.delegate = project
	prjClosure.resolveStrategy = Closure.DELEGATE_FIRST
	prjClosure()
	
//	def extClosure = binding.ext.clone()
//	extClosure.delegate = project.ext
//	extClosure.resolveStrategy = Closure.DELEGATE_FIRST
//	extClosure()
}

configureEnvironment()

def configureEnvironment() {
    def eei = EclipseEnvironmentInfo.getDefault()
    if (!ext.properties.containsKey('osgiOS')) {
        ext.osgiOS = eei.getOS()
    }
    if (!ext.properties.containsKey('osgiWS')) {
        ext.osgiWS = eei.getWS()
    }
    if (!ext.properties.containsKey('osgiArch')) {
        ext.osgiArch = eei.getOSArch()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

task cli {
    // configure this task. the CommandLineBuilder will add the
    // actual task to execute as a dependency to this task
    if (project.ext.properties.containsKey("args")) {
        new CommandLineBuilder(project).run(project.ext.args)
    }
}

clean << {
    project.delete(new File(project.rootDir, 'ObjectStore'))
    project.delete(new File(project.rootDir, 'target'))
    project.delete(new File(project.ext.rootDir, 'pom.xml'))
    project.delete(new File(project.ext.platformBundle, 'pom.xml'))

    // Make sure no tycho artifacts survive cleaning.
    new BundleParser(project).getParsedBundles().each { b ->
        println("Cleaning ${b.key}")
        project.delete(new File(b.value.path, '/target'))
        project.delete(new File(b.value.path, '/pom.xml'))
        // bin folder is not used by tycho
    }
}
